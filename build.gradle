buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.0-beta-1038'
  }
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'

group   = 'com.github.plombardi89'
version = '0.0.1'

ext {
  argparse4jVersion = "0.6.0"
  assertjVersion = "3.2.0"
  hazelcastVersion = '3.5.3'
  jacksonVersion = '2.6.3'
  jacksonKotlinVersion = '2.6.3-2'
  junitVersion  = '4.12'
  kotlinVersion = '1.0.0-beta-1038'
  logbackVersion = '1.1.3'
  minimalJsonVersion = '0.9.3'
  slf4jVersion = '1.7.12'
  spekVersion = '0.1.180'
  vertxVersion = "3.1.0"
}

sourceSets {
  integTest {
    kotlin.srcDir file('src/integTest/kotlin')
    resources.srcDir file('src/integTest/resources')
  }
  main.java.srcDirs += 'src/main/kotlin'
}

repositories {
  jcenter()
  maven {
    url "http://repository.jetbrains.com/all"
  }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
  compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

  compile "io.vertx:vertx-core:${vertxVersion}"
  compile "io.vertx:vertx-web:${vertxVersion}"
  compile "net.sourceforge.argparse4j:argparse4j:${argparse4jVersion}"
  compile "org.slf4j:slf4j-api:${slf4jVersion}"
  compile "ch.qos.logback:logback-classic:${logbackVersion}"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonKotlinVersion}"

  testCompile "junit:junit:${junitVersion}"
  testCompile "org.jetbrains.spek:spek:${spekVersion}"
  testCompile "org.assertj:assertj-core:${assertjVersion}"

  integTestCompile sourceSets.main.output
  integTestCompile configurations.testCompile
  integTestCompile sourceSets.test.output
  integTestRuntime configurations.testRuntime
}

task integTest(type: Test) {
  testClassesDir = sourceSets.integTest.output.classesDir
  classpath = sourceSets.integTest.runtimeClasspath
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.5'
}
